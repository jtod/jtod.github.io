; Program ArrayAddk

; Simple example of array traversal: traverse an array x that contains
; n elements, and add k to each element of the array.

; The algorithm
;   i := 0
;   while i < n do
;     x[i] := x[i] + k
;     i := i+1

; Register usage
;   R1 = i  loop counter
;   R2 = 1  constant for incrementing loop counter
;   R3 = n  size of array
;   R4 = k  constant to add to elements
;   R5      temporary

; Initialisation
      lea    R1,0[R0]     ; i := 0
      lea    R2,1[R0]     ; R2 := 1
      load   R3,n[R0]     ; R3 := n
      load   R4,k[R0]     ; R4 := k

loop  cmplt  R5,R1,R3     ; R5 := i < n
      jumpf  R5,done[R0]  ; if not (i<n) then goto done
      load   R5,x[R1]     ; R5 := x[i]
      add    R5,R5,R4     ; R5 := x[i] + k
      store  R5,x[R1]     ; x[i] := x[i] + k
      add    R1,R1,R2     ; i := i + 1
      jump   loop[R0]     ; goto loop

done  trap   R0,R0,R0     ; terminate the program

k     data    7           ; value to add to elements
n     data    4           ; size of array x
x     data   17
      data   23
      data  981
      data   76
